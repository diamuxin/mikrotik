:log info "Automated Cloud Backup Started"
:local DeviceName [/system identity get name]
:local mydatetime

/system backup cloud upload-file action=create-and-upload password=myBestPassword replace=[find name]
:delay 2s
/system backup cloud print

# Calculate the time difference from GMT
/system clock
:local vgmt  [:tonum [get gmt-offset]]; :if ($vgmt > 86400) do={:set vgmt ($vgmt - 4294967296)}
:local Fzerofill do={:return [:pick (100 + $1) 1 3]}
:local gmtSg "+"; :if ($vgmt < 0) do={:set gmtSg "-"; :set vgmt ($vgmt * -1)}
:local gmtHr [$Fzerofill ($vgmt / 3600)]
:local Z    "$gmtSg$gmtHr"
:put $Z

# format: nov/26/2022 00:36:26
:local date [/system backup cloud get 0 date]
:local month [:tostr ([:find "janfebmaraprmayjunjulaugsepoctnovdec" [:pick $date 0 3] ]/3+1)]
:if ([:tonum $month]<10) do={:set month "0$month"}
:set mydatetime ( [:pick $date 4 6]."/".$month."/".[:pick $date 7 11]."-".([:pick $date 12 14]$Z).":".[:pick $date 15 17].":".[:pick $date 18 20] )

:local dateCloud $mydatetime
:local secretCloud [/system backup cloud get 0 secret-download-key]

# START SEND TELEGRAM
:local MessageText "\F0\9F\92\BE <b>$DeviceName:</b> Cloud Backup OK%0ADate: $dateCloud%0ASecret Download Key: $secretCloud"
:local SendTelegramMessage [:parse [/system script get MyTGBotSendMessage source]]
$SendTelegramMessage MessageText=$MessageText
# END SEND TELEGRAM

:log info "Automated Cloud Backup Completed"
